
model {
    # Likelihood:
    for (i in 1:N){
      outcome[i] ~ dbern(mu[i]) 
      
      mu[i] <- a[admission_ward_index[i]] +   # intercept - each ward has an intercept 
        b[admission_ward_index[i]] * foi_comm[i] + # each ward has a slope 
        c[admission_ward_index[i]] * foi_noso[i]  + 
        d[admission_ward_index[i]] * foi_staff[i] + 
        e[admission_ward_index[i]] * foi_comm_admit[i] 
      
      loglike[i] <- dbin(outcome[i], mu[i], 1) # For WAIC computation
    }
    
    for (w in 1:N_ward){
      a[w] <- a0 + aprimed[w] * sigma.a
      b[w] <- b0 + bprimed[w] * sigma.b
      c[w] <- c0 + cprimed[w] * sigma.c
      d[w] <- d0 + dprimed[w] * sigma.d
      e[w] <- e0 + eprimed[w] * sigma.e
      aprimed[w] ~ dnorm(0, 1) T(0,)
      bprimed[w] ~ dnorm(0, 1) T(0,)
      cprimed[w] ~ dnorm(0, 1) T(0,)
      dprimed[w] ~ dnorm(0, 1) T(0,)
      eprimed[w] ~ dnorm(0, 1) T(0,)
    }
    
    # Priors:
    a0 ~ dnorm(0, sd_prior) T(0,)
    sigma.a ~ dunif(0, 1);
    b0 ~ dnorm(mean_prior, sd_prior) T(0,)
    sigma.b ~ dunif(0, 1);
    c0 ~ dnorm(mean_prior, sd_prior) T(0,)
    sigma.c ~ dunif(0, 1);
    d0 ~ dnorm(mean_prior, sd_prior) T(0,)
    sigma.d ~ dunif(0, 1);
    e0 ~ dnorm(mean_prior, sd_prior) T(0,)
    sigma.e ~ dunif(0, 1);
}

